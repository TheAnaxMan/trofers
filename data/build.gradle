buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

evaluationDependsOn(':common')
evaluationDependsOn(':forge')

minecraft {

    mappings channel: 'official', version: "${rootProject.minecraft_version}"

    runs {
        data {
            ideaModule "${rootProject.name}.${project.name}.main"
            workingDirectory file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            args '--mod', rootProject.mod_id
            args '--client', '--server', '--validate'
            args '--output', project(':common').file('src/generated/resources')
            args '--existing', project(':common').file('src/main/resources')
            forceExit false

            mods {
                "${rootProject.mod_id}" {
                    source sourceSets.main
                    source project(':common').sourceSets.main
                    source project(':forge').sourceSets.main
                }
            }
        }
    }
}

mixin {
    config "mixins.${mod_id}.common.json"
}

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        // thermal
        url 'https://maven.covers1624.net/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${rootProject.forge_version}"
    implementation fg.deobf("dev.architectury:architectury-forge:$architectury_version")
    implementation(project(path: ':common', configuration: 'namedElements')) { transitive false }
    implementation(project(path: ':forge', configuration: 'namedElements')) { transitive false }

    implementation fg.deobf("curse.maven:citadel-331936:4473245")
    implementation fg.deobf("curse.maven:alexsmobs-426558:4476475")

    compileOnly fg.deobf("com.teamcofh:cofh_core:1.18.2-1.6.3.19")
    compileOnly fg.deobf("com.teamcofh:thermal_foundation:1.18.2-1.6.2.27")

    compileOnly fg.deobf("curse.maven:mantle-74924:4353120")
    compileOnly fg.deobf("curse.maven:tconstruct-74072:4395997")

    compileOnly fg.deobf("curse.maven:autoreglib-250363:4100299")
    compileOnly fg.deobf("curse.maven:quark-243121:4480516")
}

jar.finalizedBy('reobfJar')

idea.module {
    excludeDirs += [ file('run') ]
}